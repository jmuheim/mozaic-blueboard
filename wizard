// Make sure to start the BlueBoard while pressing the B button!
@OnLoad
  SetShortName {GTL Wizard}

  Call @InitConstants
  Call @InitVariables
  Call @InitGtlMidiCodes

  Call @IlluminateAllbuttons
  Call @AskForInput
@End

@InitConstants
  UNDEFINED = -1

  BB_NOTE_A = 60
  BB_NOTE_B = 62
  BB_NOTE_C = 64
  BB_NOTE_D = 65

  1_BEAT_TAP_TIME   = 1000 / (HostTempo / 60)
  4_BEATS_TAP_TIME  = 4 * 1_BEAT_TAP_TIME

  DEFAULT_GTL_CLOCK_LENGTH = 4 // Make sure that GTL has this clock length set when starting a performance!

  TAP_IMMEDIATE = 0
  TAP_1_BAR  = 1
  TAP_4_BARS = 2

  STEP_LOOP = 1
  LOOP_1 = 1
  LOOP_2 = 2
  LOOP_3 = 3
  LOOP_4 = 4

  STEP_GROUP = 2
  GROUP_M = 1
  GROUP_1 = 2
  GROUP_2 = 3
  GROUP_3 = 4
  GROUP_4 = 5
  GROUP_5 = 6

  STEP_CLOCK_LENGTH = 3
  CLOCK_LENGTH_2  = 2
  CLOCK_LENGTH_4  = 4
  CLOCK_LENGTH_8  = 8
  CLOCK_LENGTH_10 = 10
  CLOCK_LENGTH_12 = 12
  CLOCK_LENGTH_16 = 16

  STEP_REC_SENDS = 4
  REC_SENDS_GUITAR           = 1
  REC_SENDS_MIC              = 2
  REC_SENDS_MIC_AND_GUITAR   = 3
  REC_SENDS_KEYBOARD         = 4
  REC_SENDS_MIC_AND_KEYBOARD = 5

  STEP_INSTR_FX = 5
  INSTR_FX_1 = 1
  INSTR_FX_2 = 2
  INSTR_FX_3 = 3
  INSTR_FX_4 = 4
  INSTR_FX_5 = 5
  INSTR_FX_6 = 6

  STEP_REC_MODE = 6
  REC_MODE_RECORD_OVERDUB     = 1
  REC_MODE_RECORD_MUTE        = 2
  REC_MODE_RECORD_SELECT_NEXT = 3

  FIRST_STEP = STEP_LOOP
  LAST_STEP  = STEP_GROUP

  GTL_CHANNEL = 0 // Will be displayed as channel 1 in GTL!
  BB_CHANNEL  = 0

  ON_OFF_DELAY = 0
@End

// These are the codes that are sent as MIDI to GTL.
// GTL needs to have respective MIDI bindings assigned to these codes!
@InitGtlMidiCodes
  GTL_SELECT_GROUP_M = 48
  GTL_SELECT_GROUP_1 = 49
  GTL_SELECT_GROUP_2 = 50
  GTL_SELECT_GROUP_3 = 51
  GTL_SELECT_GROUP_4 = 52
  GTL_SELECT_GROUP_5 = 53

  GTL_SELECT_LOOP_1 = 54
  GTL_SELECT_LOOP_2 = 55
  GTL_SELECT_LOOP_3 = 56
  GTL_SELECT_LOOP_4 = 57

  GTL_DECREASE_CLOCK = 58
  GTL_INCREASE_CLOCK = 59

  GTL_RECORD_OVERDUB     = 60
  GTL_RECORD_MUTE        = 61
  GTL_RECORD_SELECT_NEXT = 62
@End

@InitVariables
  stackSize = 0
  lastActivatedStackId = 0
  currentStep = FIRST_STEP

  currentClockLength = DEFAULT_GTL_CLOCK_LENGTH

  lastSelectedGroup       = UNDEFINED
  lastSelectedLoop        = UNDEFINED
  lastSelectedClockLength = UNDEFINED
  lastSelectedRecSends    = UNDEFINED
  lastSelectedInstrFx     = UNDEFINED
  lastSelectedRecMode     = UNDEFINED

  groupStack       = []
  loopStack        = []
  clockLengthStack = []
  gtlSendsStack    = []
  instrFxStack     = []
  recModeStack     = []

  // Uncomment this to run through some settings quickly, so you can check whether they are applied correctly
  Call @InitTestData
@End

@InitTestData
  groupStack[0]       = GROUP_3
  loopStack[0]        = LOOP_1
  clockLengthStack[0] = CLOCK_LENGTH_2
  gtlSendsStack[0]    = 2
  instrFxStack[0]     = 1
  recModeStack[0]     = REC_MODE_RECORD_MUTE

  groupStack[1]       = GROUP_M
  loopStack[1]        = LOOP_2
  clockLengthStack[1] = CLOCK_LENGTH_8
  gtlSendsStack[1]    = 2
  instrFxStack[1]     = 1
  recModeStack[1]     = REC_MODE_RECORD_OVERDUB

  groupStack[2]       = GROUP_3
  loopStack[2]        = LOOP_1
  clockLengthStack[2] = CLOCK_LENGTH_2
  gtlSendsStack[2]    = 2
  instrFxStack[2]     = 1
  recModeStack[2]     = REC_MODE_RECORD_OVERDUB

  groupStack[3]       = GROUP_3
  loopStack[3]        = LOOP_2
  clockLengthStack[3] = CLOCK_LENGTH_4 // Be sure that the last test case ends with DEFAULT_GTL_CLOCK_LENGTH again!
  gtlSendsStack[3]    = 2
  instrFxStack[3]     = 1
  recModeStack[3]     = REC_MODE_RECORD_SELECT_NEXT

  stackSize = 4
@End

@IlluminateAllbuttons
  SendMidiNoteOn BB_CHANNEL, BB_NOTE_A, 100
  SendMidiNoteOn BB_CHANNEL, BB_NOTE_B, 100
  SendMidiNoteOn BB_CHANNEL, BB_NOTE_C, 100
  SendMidiNoteOn BB_CHANNEL, BB_NOTE_D, 100
@End

@OnMidiNoteOn
  buttonPressedStartTime = SystemTime // Time at which button was pressed down
@End

@OnMidiNoteOff
  buttonPressedTime = SystemTime - buttonPressedStartTime

  If buttonPressedTime >= 4_BEATS_TAP_TIME
    tapType = TAP_4_BARS
  Elseif buttonPressedTime >= 1_BEAT_TAP_TIME
    tapType = TAP_1_BAR
  Else
    tapType = TAP_IMMEDIATE
  Endif

  Call @DispatchButtonPress
@End

@DispatchButtonPress
  wrongInputDetected = YES

  if MidiNote = BB_NOTE_D
    if tapType = TAP_IMMEDIATE
      Call @ArmCycle
    elseif tapType = TAP_4_BARS
      Call @AddCycle
    endif
  else
    if currentStep = STEP_LOOP
      Call @MapUserInputToLoop
    elseif currentStep = STEP_GROUP
      Call @MapUserInputToGroup
    elseif currentStep = STEP_CLOCK_LENGTH
      Call @MapUserInputToClockLength
    elseif currentStep = STEP_REC_SENDS
      Call @MapUserInputToRecSends
    elseif currentStep = STEP_INSTR_FX
      Call @MapUserInputToInstrFx
    elseif currentStep = STEP_REC_MODE
      Call @MapUserInputToRecMode
    endif

    if wrongInputDetected = YES
      Log {Wrong input! Try again.}
    else
      if currentStep < LAST_STEP
        Inc currentStep
      else
        Call @AddCycle
      endif

      Call @AskForInput
    endif
  endif
@End

@MapUserInputToRecSends
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedRecSends = REC_SENDS_GUITAR
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_B
      lastSelectedRecSends = REC_SENDS_MIC
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_C
      lastSelectedRecSends = REC_SENDS_MIC_AND_GUITAR
      wrongInputDetected = NO
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedRecSends = REC_SENDS_KEYBOARD
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_B
      // Some day maybe some voice FX, e.g. Vocoder?
    elseif MidiNote = BB_NOTE_C
      lastSelectedRecSends = REC_SENDS_MIC_AND_KEYBOARD
      wrongInputDetected = NO
    endif
  endif
@End

@MapUserInputToRecMode
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedRecMode = REC_MODE_RECORD_OVERDUB
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_B
      lastSelectedRecMode = REC_MODE_RECORD_MUTE
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_C
      lastSelectedRecMode = REC_MODE_RECORD_SELECT_NEXT
      wrongInputDetected = NO
    endif
  elseif tapType = TAP_1_BAR
    // TODO: Advanced stuff!
  endif
@End

@MapUserInputToInstrFx
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedInstrFx = INSTR_FX_1
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_B
      lastSelectedInstrFx = INSTR_FX_2
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_C
      lastSelectedInstrFx = INSTR_FX_3
      wrongInputDetected = NO
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedInstrFx = INSTR_FX_4
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_B
      lastSelectedInstrFx = INSTR_FX_5
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_C
      lastSelectedInstrFx = INSTR_FX_6
      wrongInputDetected = NO
    endif
  endif
@End

@MapUserInputToLoop
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedLoop = LOOP_1
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_B
      lastSelectedLoop = LOOP_2
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_C
      lastSelectedLoop = LOOP_3
      wrongInputDetected = NO
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedLoop = LOOP_4
      wrongInputDetected = NO
    endif
  endif
@End

@MapUserInputToClockLength
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedClockLength = CLOCK_LENGTH_2
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_B
      lastSelectedClockLength = CLOCK_LENGTH_4
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_C
      lastSelectedClockLength = CLOCK_LENGTH_8
      wrongInputDetected = NO
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedClockLength = CLOCK_LENGTH_10
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_B
      lastSelectedClockLength = CLOCK_LENGTH_12
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_C
      lastSelectedClockLength = CLOCK_LENGTH_16
      wrongInputDetected = NO
    endif
  endif
@End

@MapUserInputToGroup
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedGroup = GROUP_1
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_B
      lastSelectedGroup = GROUP_2
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_C
      lastSelectedGroup = GROUP_3
      wrongInputDetected = NO
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedGroup = GROUP_M
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_B
      lastSelectedGroup = GROUP_4
      wrongInputDetected = NO
    elseif MidiNote = BB_NOTE_C
      lastSelectedGroup = GROUP_5
      wrongInputDetected = NO
    endif
  endif
@End

@AskForInput
  if currentStep = STEP_GROUP
    Log {Please select group.}
  elseif currentStep = STEP_LOOP
    Log {Please select loop.}
  elseif currentStep = STEP_CLOCK_LENGTH
    Log {Please select clock length.}
  elseif currentStep = STEP_REC_SENDS
    Log {Please select GTL sends.}
  elseif currentStep = STEP_INSTR_FX
    Log {Please select instr fx.}
  elseif currentStep = STEP_REC_MODE
    Log {Please select record mode.}
  endif
@End

@AddCycle
  configPartMissing = NO

  if lastSelectedLoop = UNDEFINED
    configPartMissing = YES
    Log {No loop selected yet}
  else
    loopStack[stackSize] = lastSelectedLoop
  endif

  if lastSelectedGroup = UNDEFINED
    configPartMissing = YES
    Log {No group selected yet}
  else
    groupStack[stackSize] = lastSelectedGroup
  endif

  if lastSelectedClockLength = UNDEFINED
    configPartMissing = YES
    Log {No clock length selected yet}
  else
    clockLengthStack[stackSize] = lastSelectedClockLength
  endif

//  if lastSelectedRecSends = UNDEFINED
//    configPartMissing = YES
//    Log {No GTL sends selected yet}
//  else
//    gtlSendsStack[stackSize] = lastSelectedRecSends
//  endif
//
//  if lastSelectedInstrFx = UNDEFINED
//    configPartMissing = YES
//    Log {No instr fx selected yet}
//  else
//    instrFxStack[stackSize] = lastSelectedInstrFx
//  endif

  if lastSelectedRecMode = UNDEFINED
    configPartMissing = YES
    Log {No rec mode selected yet}
  else
    recModeStack[stackSize] = lastSelectedRecMode
  endif

  if configPartMissing = NO
    currentStep = FIRST_STEP
    Inc stackSize

    Log {Cycle added!}
  endif
@End

@ArmCycle
  if stackSize = 0
    Log {No cycles provided yet! Please run through at least one configuration cycle.}
  elseif lastActivatedStackId = stackSize
    Log {No cycle left. You can add new cycles whenever you like.}
  else
    Log {Arming cycle #}, lastActivatedStackId

    Call @ArmGroup // Needs to be before @ArmLoop!
    Call @ArmLoop
    Call @ArmClockLength
//    Call @ArmRecSends
//    Call @ArmInstrFx
    Call @ArmRecMode

    Inc lastActivatedStackId
  endif
@End

@ArmLoop
  newLoop = loopStack[lastActivatedStackId]
  Log {Current loop: }, newLoop

  if newLoop = LOOP_1
    midiToSend = GTL_SELECT_LOOP_1
  elseif newLoop = LOOP_2
    midiToSend = GTL_SELECT_LOOP_2
  elseif newLoop = LOOP_3
    midiToSend = GTL_SELECT_LOOP_3
  elseif newLoop = LOOP_4
    midiToSend = GTL_SELECT_LOOP_4
  endif

  Log {Selecting loop by sending MIDI to GTL: }, midiToSend

  SendMidiNoteOn  GTL_CHANNEL, midiToSend, 100
  SendMidiNoteOff GTL_CHANNEL, midiToSend, 0, ON_OFF_DELAY
@End

@ArmGroup
  newGroup = groupStack[lastActivatedStackId]

  if newGroup = GROUP_M
    midiToSend = GTL_SELECT_GROUP_M
  elseif newGroup = GROUP_1
    midiToSend = GTL_SELECT_GROUP_1
  elseif newGroup = GROUP_2
    midiToSend = GTL_SELECT_GROUP_2
  elseif newGroup = GROUP_3
    midiToSend = GTL_SELECT_GROUP_3
  elseif newGroup = GROUP_4
    midiToSend = GTL_SELECT_GROUP_4
  elseif newGroup = GROUP_5
    midiToSend = GTL_SELECT_GROUP_5
  endif

  Log {Selecting group by sending MIDI to GTL: }, midiToSend

  SendMidiNoteOn  GTL_CHANNEL, midiToSend, 100
  SendMidiNoteOff GTL_CHANNEL, midiToSend, 0, ON_OFF_DELAY
@End

@ArmClockLength
  newClockLength = clockLengthStack[lastActivatedStackId]
  difference = newClockLength - currentClockLength

  if difference >= 0
    midiToSend = GTL_INCREASE_CLOCK
    timesToSend = difference
  elseif difference < 0
    midiToSend = GTL_DECREASE_CLOCK
    timesToSend = difference * -1
  Endif

  Log {Setting clock length from }, currentClockLength, { to }, newClockLength, { by sending MIDI to GTL: }, midiToSend, { (*}, timesToSend, {)}

  for i = 1 to timesToSend
    SendMidiNoteOn  GTL_CHANNEL, midiToSend, 100
    SendMidiNoteOff GTL_CHANNEL, midiToSend, 0, ON_OFF_DELAY
  endfor

  currentClockLength = newClockLength
@End

@ArmRecSends
  newRecSends = groupStack[lastActivatedStackId]
  Log {- GTL sends: }, newRecSends
@End

@ArmInstrFx
  newInstrFx = instrFxStack[lastActivatedStackId]
  Log {- Instr fx: }, newInstrFx
@End

@ArmRecMode
  newRecMode = recModeStack[lastActivatedStackId]

  if newRecMode = LOOP_1
    midiToSend = GTL_RECORD_OVERDUB
  elseif newRecMode = LOOP_2
    midiToSend = GTL_RECORD_MUTE
  elseif newRecMode = LOOP_3
    midiToSend = GTL_RECORD_SELECT_NEXT
  endif

  Log {Starting record mode by sending MIDI to GTL: }, midiToSend

  // SendMidiNoteOn  GTL_CHANNEL, midiToSend, 100
  // SendMidiNoteOff GTL_CHANNEL, midiToSend, 0, ON_OFF_DELAY
@End
