// Make sure to start the BlueBoard while pressing the B button!
@OnLoad
  SetShortName {Wizard}

  Call @InitConstants
  Call @InitVariables

  Call @IlluminateAllbuttons
  Call @AskForInput
@End

@InitConstants
  BB_NOTE_A = 60
  BB_NOTE_B = 62
  BB_NOTE_C = 64
  BB_NOTE_D = 65

  UNDEFINED = -1

  1_BEAT_TAP_TIME   = 1000 / (HostTempo / 60)
  2_BEATS_TAP_TIME  = 2 * 1_BEAT_TAP_TIME
  4_BEATS_TAP_TIME = 4 * 1_BEAT_TAP_TIME

  TAP_IMMEDIATE = 1
  TAP_1_BAR  = 1
  TAP_2_BARS = 2
  TAP_4_BARS = 3

  STEP_LOOP = 1
  LOOP_1 = 1
  LOOP_2 = 2
  LOOP_3 = 3
  LOOP_4 = 4

  STEP_GROUP = 2
  GROUP_M = 1
  GROUP_1 = 2
  GROUP_2 = 3
  GROUP_3 = 4
  GROUP_4 = 5
  GROUP_5 = 6

  STEP_CLOCK_LENGTH = 3
  CLOCK_LENGTH_2  = 2
  CLOCK_LENGTH_4  = 4
  CLOCK_LENGTH_8  = 8
  CLOCK_LENGTH_10 = 10
  CLOCK_LENGTH_12 = 12
  CLOCK_LENGTH_16 = 16

  STEP_GTL_SENDS = 4
  GTL_SENDS_GUITAR           = 1
  GTL_SENDS_MIC              = 2
  GTL_SENDS_MIC_AND_GUITAR   = 3
  GTL_SENDS_KEYBOARD         = 4
  GTL_SENDS_MIC_AND_KEYBOARD = 5

  STEP_INSTR_FX = 5
  INSTR_FX_1 = 1
  INSTR_FX_2 = 2
  INSTR_FX_3 = 3
  INSTR_FX_4 = 4
  INSTR_FX_5 = 5
  INSTR_FX_6 = 6

  STEP_REC_MODE = 6
  REC_MODE_RECORD          = 1
  REC_MODE_OVERDUB         = 2
  REC_MODE_RECORD_AND_MUTE = 3

  FIRST_STEP = STEP_LOOP
  LAST_STEP  = STEP_REC_MODE

  GTL_CHANNEL = 15
  BB_CHANNEL  = 0
@End

@InitVariables
  cycleLength = 0
  lastActivatedCycle = 0
  currentStep = FIRST_STEP

  lastSelectedGroup       = UNDEFINED
  lastSelectedLoop        = UNDEFINED
  lastSelectedClockLength = UNDEFINED
  lastSelectedGtlSends    = UNDEFINED
  lastSelectedInstrFx     = UNDEFINED
  lastSelectedRecMode     = UNDEFINED

  groupInputs       = []
  loopInputs        = []
  clockLengthInputs = []
  gtlSends          = []
  instrFxInputs     = []
  recModeInputs     = []
@End

@IlluminateAllbuttons
  SendMidiNoteOn BB_CHANNEL, BB_NOTE_A, 100
  SendMidiNoteOn BB_CHANNEL, BB_NOTE_B, 100
  SendMidiNoteOn BB_CHANNEL, BB_NOTE_C, 100
  SendMidiNoteOn BB_CHANNEL, BB_NOTE_D, 100
@End

@OnMidiNoteOn
  buttonPressedStartTime = SystemTime // Time at which button was pressed down
@End

@OnMidiNoteOff
  buttonPressedTime = SystemTime - buttonPressedStartTime

  If buttonPressedTime >= 4_BEATS_TAP_TIME
    tapType = TAP_4_BARS
  Elseif buttonPressedTime >= 2_BEATS_TAP_TIME
    tapType = TAP_2_BARS
  Elseif buttonPressedTime >= 1_BEAT_TAP_TIME
    tapType = TAP_1_BAR
  Else
    tapType = TAP_IMMEDIATE
  Endif

  Call @DispatchButtonPress
@End

@DispatchButtonPress
  if MidiNote = BB_NOTE_D
    if tapType = TAP_IMMEDIATE
      Call @ArmCycle
    elseif tapType = TAP_4_BARS
      Call @AddCycle
    endif
  else
    if currentStep = STEP_LOOP
      Call @MapUserInputToLoop
    elseif currentStep = STEP_GROUP
      Call @MapUserInputToGroup
    elseif currentStep = STEP_CLOCK_LENGTH
      Call @MapUserInputToClockLength
    elseif currentStep = STEP_GTL_SENDS
      Call @MapUserInputToGtlSends
    elseif currentStep = STEP_INSTR_FX
      Call @MapUserInputToInstrFx
    elseif currentStep = STEP_REC_MODE
      Call @MapUserInputToRecMode
    endif

    if currentStep < LAST_STEP
      Inc currentStep
    else
      Call @AddCycle
    endif

    Call @AskForInput
  endif
@End

@AddCycle
  configPartMissing = NO

  if lastSelectedGtlSends = UNDEFINED
    configPartMissing = YES
    Log {No GTL sends selected yet}
  else
    lastSelectedGtlSendsInputs[cycleLength] = lastSelectedGtlSends
  endif

  if lastSelectedRecMode = UNDEFINED
    configPartMissing = YES
    Log {No rec mode selected yet}
  else
    lastSelectedRecModeInputs[cycleLength] = lastSelectedRecMode
  endif

  if lastSelectedInstrFx = UNDEFINED
    configPartMissing = YES
    Log {No instr fx selected yet}
  else
    lastSelectedInstrFxInputs[cycleLength] = lastSelectedInstrFx
  endif

  if lastSelectedLoop = UNDEFINED
    configPartMissing = YES
    Log {No loop selected yet}
  else
    lastSelectedLoopInputs[cycleLength] = lastSelectedLoop
  endif

  if lastSelectedClockLength = UNDEFINED
    configPartMissing = YES
    Log {No clock length selected yet}
  else
    lastSelectedClockLengthInputs[cycleLength] = lastSelectedClockLength
  endif

  if lastSelectedGroup = UNDEFINED
    configPartMissing = YES
    Log {No group selected yet}
  else
    lastSelectedGroupInputs[cycleLength] = lastSelectedGroup
  endif

  if configPartMissing = NO
    currentStep = FIRST_STEP
    Inc cycleLength

    Log {Cycle added!}
  endif
@End

@MapUserInputToGtlSends
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedGtlSends = GTL_SENDS_GUITAR
    elseif MidiNote = BB_NOTE_B
      lastSelectedGtlSends = GTL_SENDS_MIC
    elseif MidiNote = BB_NOTE_C
      lastSelectedGtlSends = GTL_SENDS_MIC_AND_GUITAR
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedGtlSends = GTL_SENDS_KEYBOARD
    elseif MidiNote = BB_NOTE_B
      // Some day maybe some voice FX, e.g. Vocoder?
    elseif MidiNote = BB_NOTE_C
      lastSelectedGtlSends = GTL_SENDS_MIC_AND_KEYBOARD
    endif
  endif
@End

@MapUserInputToRecMode
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedRecMode = REC_MODE_RECORD
    elseif MidiNote = BB_NOTE_B
      lastSelectedRecMode = REC_MODE_OVERDUB
    elseif MidiNote = BB_NOTE_C
      // TODO: REC_MODE_ADVANCED
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedRecMode = REC_MODE_RECORD_AND_MUTE
    elseif MidiNote = BB_NOTE_B
      // Nothing
    elseif MidiNote = BB_NOTE_C
      // TODO: REC_MODE_ADVANCED_AND_MUTE
    endif
  endif
@End

@MapUserInputToInstrFx
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedInstrFx = INSTR_FX_1
    elseif MidiNote = BB_NOTE_B
      lastSelectedInstrFx = INSTR_FX_2
    elseif MidiNote = BB_NOTE_C
      lastSelectedInstrFx = INSTR_FX_3
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedInstrFx = INSTR_FX_4
    elseif MidiNote = BB_NOTE_B
      lastSelectedInstrFx = INSTR_FX_5
    elseif MidiNote = BB_NOTE_C
      lastSelectedInstrFx = INSTR_FX_6
    endif
  endif
@End

@MapUserInputToLoop
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedLoop = LOOP_1
    elseif MidiNote = BB_NOTE_B
      lastSelectedLoop = LOOP_2
    elseif MidiNote = BB_NOTE_C
      lastSelectedLoop = LOOP_3
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedLoop = LOOP_4
    endif
  endif
@End

@MapUserInputToClockLength
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedClockLength = CLOCK_LENGTH_2
    elseif MidiNote = BB_NOTE_B
      lastSelectedClockLength = CLOCK_LENGTH_4
    elseif MidiNote = BB_NOTE_C
      lastSelectedClockLength = CLOCK_LENGTH_8
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedClockLength = CLOCK_LENGTH_10
    elseif MidiNote = BB_NOTE_B
      lastSelectedClockLength = CLOCK_LENGTH_12
    elseif MidiNote = BB_NOTE_C
      lastSelectedClockLength = CLOCK_LENGTH_16
    endif
  endif
@End

@MapUserInputToGroup
  if tapType = TAP_IMMEDIATE
    if MidiNote = BB_NOTE_A
      lastSelectedGroup = GROUP_1
    elseif MidiNote = BB_NOTE_B
      lastSelectedGroup = GROUP_2
    elseif MidiNote = BB_NOTE_C
      lastSelectedGroup = GROUP_3
    endif
  elseif tapType = TAP_1_BAR
    if MidiNote = BB_NOTE_A
      lastSelectedGroup = GROUP_M
    elseif MidiNote = BB_NOTE_B
      lastSelectedGroup = GROUP_4
    elseif MidiNote = BB_NOTE_C
      lastSelectedGroup = GROUP_5
    endif
  endif
@End

@AskForInput
  if currentStep = STEP_GROUP
    Log {Please select group}
  elseif currentStep = STEP_LOOP
    Log {Please select loop}
  elseif currentStep = STEP_CLOCK_LENGTH
    Log {Please select clock length}
  elseif currentStep = STEP_GTL_SENDS
    Log {Please select GTL input}
  elseif currentStep = STEP_INSTR_FX
    Log {Please select instr fx}
  elseif currentStep = STEP_REC_MODE
    Log {Please select record mode}
  endif
@End

@ArmCycle
  if cycleLength = 0
    Log {No cycles provided yet}
  elseif lastActivatedCycle = cycleLength
    Log {No cycle left}
  else
    Log {Arming cycle #}, lastActivatedCycle, {:}
    Log {- Group: }, lastSelectedGroupInputs[lastActivatedCycle]
    Log {- Loop: }, lastSelectedLoopInputs[lastActivatedCycle]
    Log {- Clock length: }, lastSelectedClockLengthInputs[lastActivatedCycle]
    Log {- GTL sends: }, lastSelectedGroupInputs[lastActivatedCycle]
    Log {- Instr fx: }, lastSelectedInstrFxInputs[lastActivatedCycle]
    Log {- Rec mode: }, lastSelectedRecModeInputs[lastActivatedCycle]

    Inc lastActivatedCycle
  endif
@End
