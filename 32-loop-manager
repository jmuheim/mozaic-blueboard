// AUM routing:
// - Receives from StepSequencer

@OnLoad
  SetShortName {32-LoopManager}

  Call @InitSharedConstants
  Call @InitConstants
  Call @Reset
@End

@InitConstants
  LOOP_1 = 1
  LOOP_2 = 2
  LOOP_3 = 3
  LOOP_4 = 4
@End

@InitVariables
  lastSelectedLoop = UNDEFINED
  loopStack = []
  loopStackSize = 0
@End

@Reset
  Log {Reset}
  Call @InitVariables
@End

@OnSysex
  ReceiveSysex data
  sysexTarget = data[0]
  sysexAction = data[1]

  if sysexTarget = MOZ_INST_LOOP_MANAGER
    if sysexAction = SYSEX_DISPATCH_BUTTON
      bbButton = data[2]
      tapType  = data[3]

      Log {Received button=}, bbButton, {, tapType=}, tapType

      Call @DispatchButtonPress
    elseif sysexAction = SYSEX_PROCEED_WITH_WORKFLOW
      Call @ProceedWithWorkflow
    elseif sysexAction = SYSEX_ARM_CONFIG
      loopStackId = data[2]
      Call @ArmConfig
    elseif sysexAction = SYSEX_DUPLICATE_LAST_INPUT
      Call @DuplicateLastInput
    else
      Log {ERROR: Unknown sysexAction=}, sysexAction
    endif
  elseif sysexTarget = MOZ_INST_ALL
    if sysexAction = SYSEX_RESET_STEP_SEQUENCER
      Call @Reset
    endif
  endif
@End

@ProceedWithWorkflow
  Log {Proceeding with workflow}
  data = [MOZ_INST_FEEDBACKER, SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_SELECT, FEEDBACK_LOOP]
  SendSysex data, 4
@End

@DispatchButtonPress
  lastSelectedLoop = UNDEFINED

  if tapType = TAP_TYPE_IMMEDIATE
    if bbButton = BB_BUTTON_A
      lastSelectedLoop = LOOP_1
    elseif bbButton = BB_BUTTON_B
      lastSelectedLoop = LOOP_2
    elseif bbButton = BB_BUTTON_C
      lastSelectedLoop = LOOP_3
    else
      Log {ERROR! Unknown bbButton=}, bbButton, {, tapType=}, tapType
    endif
  elseif tapType = TAP_TYPE_1_BEAT
    if bbButton = BB_BUTTON_A
      lastSelectedLoop = LOOP_4
    else
      Log {ERROR! Unknown bbButton=}, bbButton, {, tapType=}, tapType
    endif
  else
    Log {ERROR! Unknown tapType=}, tapType
  endif

  if lastSelectedLoop = UNDEFINED
    Log {Unknown group input, try again!}

    Call @SayUnknownLoop
    Call @SayTryAgain
  else
    Call @SayLoopSelected
    Call @AddLoopToStack
  endif
@End

@SayUnknownLoop
  data = [MOZ_INST_FEEDBACKER, SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_UNKNOWN, FEEDBACK_LOOP]
  SendSysex data, 4
@End

@SayTryAgain
  data = [MOZ_INST_FEEDBACKER, SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_TRY_AGAIN]
  SendSysex data, 3
@End

@SayLoopSelected
  data = [MOZ_INST_FEEDBACKER, SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_LOOP, lastSelectedLoop, FEEDBACK_SELECTED]
  SendSysex data, 5
@End

@AddLoopToStack
  Log {Adding loop to loopStack[}, loopStackSize, {] = }, lastSelectedLoop
  loopStack[loopStackSize] = lastSelectedLoop
  Inc loopStackSize

  Call @ConfirmLoopSelection
@End

@DuplicateLastInput
  Log {Duplicating loop to loopStack[}, loopStackSize, {] = }, lastSelectedLoop
  loopStack[loopStackSize] = lastSelectedLoop
  Inc loopStackSize
@End

@ConfirmLoopSelection
  data = [MOZ_INST_STEP_SEQUENCER, SYSEX_SELECTION_CONFIRMATION, MOZ_INST_LOOP_MANAGER]
  SendSysex data, 3
@End

@ArmConfig
  Log {Arming config with loopStack[}, loopStackId, {] = }, loopStack[loopStackId]

  data = [MOZ_INST_GTL_PROXY, SYSEX_GTL_SELECT_LOOP, loopStack[loopStackId]]
  SendSysex data, 3
@End




//////////////////////////////////////////////////////////
// These constants are shared across all Mozaic scripts //
//////////////////////////////////////////////////////////
@InitSharedConstants
  UNDEFINED = -1

  MOZ_INST_STEP_SEQUENCER = 21
  MOZ_INST_LOOP_MANAGER   = 31
  MOZ_INST_GTL_PROXY      = 92
  MOZ_INST_FEEDBACKER     = 93
  MOZ_INST_ALL            = 100

  SYSEX_DISPATCH_BUTTON        = 0
  SYSEX_PROCEED_WITH_WORKFLOW  = 1
  SYSEX_ANNOUNCE_FEEDBACK      = 2
  SYSEX_SELECTION_CONFIRMATION = 3
  SYSEX_GTL_SELECT_LOOP        = 4
  SYSEX_ARM_CONFIG             = 6
  SYSEX_RESET_STEP_SEQUENCER   = 7
  SYSEX_DUPLICATE_LAST_INPUT   = 8

  FEEDBACK_SELECT    = 15
  FEEDBACK_LOOP      = 21
  FEEDBACK_UNKNOWN   = 17
  FEEDBACK_INPUT     = 18
  FEEDBACK_TRY_AGAIN = 19
  FEEDBACK_SELECTED  = 20

  BB_BUTTON_A = 60
  BB_BUTTON_B = 62
  BB_BUTTON_C = 64
  BB_BUTTON_D = 65

  TAP_TYPE_IMMEDIATE = 0
  TAP_TYPE_1_BEAT    = 1
  TAP_TYPE_1_BAR     = 2
@End
