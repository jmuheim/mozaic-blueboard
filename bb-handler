@OnLoad
  SetShortName {BbHandler}

  Call @InitConstants
  Call @Reset
@End

@Reset
  Call @InitVariables
@End

@InitConstants
  UNDEFINED = -1

  BB_BUTTON_A = 60
  BB_BUTTON_B = 62
  BB_BUTTON_C = 64
  BB_BUTTON_D = 65
  NUMBER_OF_BB_BUTTONS = 4

  TAP_SHORT  = 0    // Up to 499ms
  TAP_MEDIUM = 500  // Between 500 and 999ms
  TAP_LONG   = 1000 // 1000ms or more
@End

@InitVariables
  buttonId = UNDEFINED
  ignoreLastButtonUp = NO

  Call @ResetButtonCombo
@End

@OnMidiNoteOn
  lastButtonDownTime = SystemTime // Time at which button was pressed down

  Call @ConvertBbMidiNoteToId
  lastButtonDown = buttonId
  Call @AppendLastButtonDownToButtonCombo

  Inc numberOfPressedButtons
@End

@OnMidiNoteOff
  lastButtonUpTime = SystemTime

  Call @ConvertBbMidiNoteToId
  lastButtonUp = buttonId
  Call @AppendLastButtonUpToButtonCombo

  Dec numberOfPressedButtons

  if numberOfPressedButtons = 0
    if ignoreLastButtonUp = NO
      Call @DispatchButtonCombo
    endif

    Call @ResetButtonCombo
    ignoreLastButtonUp = NO
  else
    if lastButtonDown = lastButtonUp
      Call @DispatchButtonCombo
      Call @RemoveLastButtonUpAndDowFromButtonCombo
      ignoreLastButtonUp = YES
    endif
  endif
@End

@ResetButtonCombo
  numberOfPressedButtons = 0
  buttonCombo = 0
@End

@DispatchButtonCombo
  Log {Firing button combo }, buttonCombo
@End

@AppendLastButtonDownToButtonCombo
  buttonCombo = buttonCombo * 10 + lastButtonDown
@End

@AppendLastButtonUpToButtonCombo
  buttonCombo = buttonCombo * 10 + lastButtonUp + NUMBER_OF_BB_BUTTONS
@End

@RemoveLastButtonUpAndDowFromButtonCombo
  buttonCombo = (buttonCombo - lastButtonUp - NUMBER_OF_BB_BUTTONS) / 10
  buttonCombo = (buttonCombo - lastButtonDown) / 10
@End

@ConvertBbMidiNoteToId
  if MidiNote = BB_BUTTON_A
    buttonId = 1
  elseif MidiNote = BB_BUTTON_B
    buttonId = 2
  elseif MidiNote = BB_BUTTON_C
    buttonId = 3
  elseif MidiNote = BB_BUTTON_D
    buttonId = 4
  else
    Log {ERROR: Wrong MidiNote=}, MidiNote
  endif
@End
