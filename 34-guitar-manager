// MIDI sources (in Mozaic's window, click the "backward S" button):
// - Receives from StepSequencer

@OnLoad
  SetShortName {34-GuitarManager}

  Call @InitSharedConstants
  Call @InitConstants
  Call @Reset
@End

@InitConstants
  GUITAR_1 = 1
  GUITAR_2 = 2
  GUITAR_3 = 3
  GUITAR_4 = 4
  GUITAR_5 = 5
  GUITAR_6 = 6
@End

@InitVariables
  lastInput = UNDEFINED
  inputStack = []
  inputStackSize = 0
@End

@Reset
  Log {Reset}
  Call @InitVariables
@End

@OnSysex
  ReceiveSysex data
  sysexTarget = data[0]
  sysexAction = data[1]

  if sysexTarget = MOZ_INST_GUITAR_MANAGER
    if sysexAction = SYSEX_DISPATCH_BUTTON
      bbButton = data[2]
      tapType  = data[3]

      Log {Received button=}, bbButton, {, tapType=}, tapType

      Call @DispatchButtonPress
    elseif sysexAction = SYSEX_PROCEED_WITH_WORKFLOW
      Call @ProceedWithWorkflow
    elseif sysexAction = SYSEX_ARM_CONFIG
      inputStackId = data[2]
      Call @ArmConfig
    elseif sysexAction = SYSEX_DUPLICATE_LAST_INPUT
      Call @DuplicateLastInput
    else
      Log {ERROR: Unknown sysexAction=}, sysexAction
    endif
  elseif sysexTarget = MOZ_INST_ALL
    if sysexAction = SYSEX_RESET_STEP_SEQUENCER
      Call @Reset
    endif
  endif
@End

@ProceedWithWorkflow
  Log {Proceeding with workflow}
  data = [MOZ_INST_FEEDBACKER, SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_SELECT, FEEDBACK_GUITAR]
  SendSysex data, 4
@End

@DispatchButtonPress
  lastInput = UNDEFINED

  if tapType = TAP_TYPE_IMMEDIATE
    if bbButton = BB_BUTTON_A
      lastInput = GUITAR_1
    elseif bbButton = BB_BUTTON_B
      lastInput = GUITAR_2
    elseif bbButton = BB_BUTTON_C
      lastInput = GUITAR_3
    else
      Log {ERROR! Unknown bbButton=}, bbButton, {, tapType=}, tapType
    endif
  elseif tapType = TAP_TYPE_1_BEAT
    if bbButton = BB_BUTTON_A
      lastInput = GUITAR_4
    elseif bbButton = BB_BUTTON_B
      lastInput = GUITAR_5
    elseif bbButton = BB_BUTTON_C
      lastInput = GUITAR_6
    else
      Log {ERROR! Unknown bbButton=}, bbButton, {, tapType=}, tapType
    endif
  else
    Log {ERROR! Unknown tapType=}, tapType
  endif

  if lastInput = UNDEFINED
    Log {Unknown input, try again!}

    Call @SayUnknownInput
    Call @SayTryAgain
  else
    Call @SayInputSelected
    Call @AddInputToStack
  endif
@End

@SayUnknownInput
  data = [MOZ_INST_FEEDBACKER, SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_UNKNOWN, FEEDBACK_GUITAR]
  SendSysex data, 4
@End

@SayTryAgain
  data = [MOZ_INST_FEEDBACKER, SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_TRY_AGAIN]
  SendSysex data, 3
@End

@SayInputSelected
  data = [MOZ_INST_FEEDBACKER, SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_GUITAR, lastInput, FEEDBACK_SELECTED]
  SendSysex data, 5
@End

@AddInputToStack
  Log {Adding loop to inputStack[}, inputStackSize, {] = }, lastInput
  inputStack[inputStackSize] = lastInput
  Inc inputStackSize

  Call @ConfirmInputSelection
@End

@DuplicateLastInput
  Log {Duplicating loop to inputStack[}, inputStackSize, {] = }, lastInput
  inputStack[inputStackSize] = lastInput
  Inc inputStackSize
@End

@ConfirmInputSelection
  data = [MOZ_INST_STEP_SEQUENCER, SYSEX_SELECTION_CONFIRMATION, MOZ_INST_GUITAR_MANAGER]
  SendSysex data, 3
@End

@ArmConfig
  Log {Arming config with inputStack[}, inputStackId, {] = }, inputStack[inputStackId]

  data = [MOZ_INST_GTL_PROXY, SYSEX_AUM_SELECT_GUITAR, inputStack[inputStackId]]
  SendSysex data, 3
@End




//////////////////////////////////////////////////////////
// These constants are shared across all Mozaic scripts //
//////////////////////////////////////////////////////////
@InitSharedConstants
  UNDEFINED = -1

  MOZ_INST_STEP_SEQUENCER =  21
  MOZ_INST_GUITAR_MANAGER =  34
  MOZ_INST_GTL_PROXY      =  92
  MOZ_INST_FEEDBACKER     =  93
  MOZ_INST_ALL            = 100

  SYSEX_DISPATCH_BUTTON        =  0
  SYSEX_PROCEED_WITH_WORKFLOW  =  1
  SYSEX_ANNOUNCE_FEEDBACK      =  2
  SYSEX_SELECTION_CONFIRMATION =  3
  SYSEX_ARM_CONFIG             =  6
  SYSEX_RESET_STEP_SEQUENCER   =  7
  SYSEX_DUPLICATE_LAST_INPUT   =  8
  SYSEX_GTL_SELECT_GUITAR      = 10

  FEEDBACK_SELECT    = 15
  FEEDBACK_UNKNOWN   = 17
  FEEDBACK_INPUT     = 18
  FEEDBACK_TRY_AGAIN = 19
  FEEDBACK_SELECTED  = 20
  FEEDBACK_GUITAR    = 31

  BB_BUTTON_A = 60
  BB_BUTTON_B = 62
  BB_BUTTON_C = 64
  BB_BUTTON_D = 65

  TAP_TYPE_IMMEDIATE = 0
  TAP_TYPE_1_BEAT    = 1
  TAP_TYPE_1_BAR     = 2
@End
