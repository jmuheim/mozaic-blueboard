@OnLoad
  SetShortName {1-LiveMode}

  DEBUG = NO
  if DEBUG = NO
    Log {Set DEBUG=YES to see runtime details}
  endif

  Call @InitSharedConstants
  Call @InitConstants
  Call @Reset

  Call @AnnounceWelcome
@End

@AnnounceWelcome
  sysexData = [SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_WELCOME]
  SendSysex sysexData, 2

  sysexData = [SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_PRESET, FEEDBACK_MODE]
  SendSysex sysexData, 3

  Call @AnnounceCurrentPreset
@End

@Reset
  Log {+---------+}
  Log {|Live-Mode|}
  Log {+---------+}

  Log {Select preset ID: Use C to increase selection (or B to decrease)!}

  Call @InitVariables
  Call @ResetGtl
@End

@InitConstants
@End

@InitVariables
  presetsSize = 2
  preset = 1
  presetSelectionConfirmed = NO

  step = 0
@End

@OnMidiNoteOn
  if DEBUG
    Log {@OnMidiNoteOn: MidiNote=}, MidiNote
  endif
@End

@OnMidiNoteOff
  if MidiNote = BB_BUTTONS_MIDI[BB_BUTTON_A]
    Call @Reset
  else
    if presetSelectionConfirmed
      Call @ProcessSelectedPreset
    else
      Call @ProcessPresetSelection
    endif
  endif

  if DEBUG
    Log {@OnMidiNoteOff: MidiNote=}, MidiNote
  endif
@End

@ProcessSelectedPreset
  if MidiNote = BB_BUTTONS_MIDI[BB_BUTTON_D]
    Inc step
    Call @ProceedWithPreset
    Call @Beep
  else
    // TODO: What other functions could be triggered using B and C buttons?
    Call @Error
  endif
@End

@ProcessPresetSelection
  if MidiNote = BB_BUTTONS_MIDI[BB_BUTTON_D]
    Call @ConfirmSelectedPreset
  else // BB_BUTTON_B or BB_BUTTON_C
    if MidiNote = BB_BUTTONS_MIDI[BB_BUTTON_B]
      Call @DecreasePreset
    else // BB_BUTTON_C
      Call @IncreasePreset
    endif

    Call @AnnounceCurrentPreset
  endif
@End

@AnnounceCurrentPreset
  sysexData = [SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_PRESET, preset]
  SendSysex sysexData, 3
@End

@IncreasePreset
  if preset = presetsSize
    Log {Current preset=}, preset, { already, which is the max! Can't increase!}
    Call @Error
  else
    Inc preset
    Log {Current preset=}, preset, {, press C again to increase, B to decrease, or D to confirm!}
  endif
@End

@DecreasePreset
  if preset = 1
    Log {Current preset=}, preset, { already, which is the min! Can't decrease!}
    Call @Error
  else
    Dec preset
    Log {Current preset=}, preset, {, press B again to decrease, C to increase, or D to confirm!}
  endif
@End

@ConfirmSelectedPreset
  presetSelectionConfirmed = YES

  Log {Preset }, preset, { selected! Press D to walk through it step by step.}

  sysexData = [SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_PRESET, preset, FEEDBACK_ACTIVATED]
  SendSysex sysexData, 4
@End

@Error
  Call @Beep
  Call @Beep
  Call @Beep
@End

@ProceedWithPreset
  //////////////////
  // Boney M. - By the Rivers of Babylon
  //////////////////
  if preset = 1
    // Intro
    if step = 1 // Mmmh‚Ä¶
      Call @PrepareGuitarPreset2 // üé∏ 2
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    
      Call @RecordNextLoopInGroup1 // ‚è∫Ô∏è 1
    elseif step = 2 // Uuuh‚Ä¶
      Call @RecordNextLoopInGroup1 // ‚è∫Ô∏è 1
      Call @PrepareKeyboardPreset1 // üéπ 1
    elseif step = 3
      Call @ToggleSendKeyboard // üéπ ‚úîÔ∏è
    
    // Chorus
    elseif step = 4 // By the rivers‚Ä¶
      Call @PrepareGuitarPreset1 // üé∏ 1
      Call @RecordNextLoopInGroup2 // ‚è∫Ô∏è 2
    elseif step = 5
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    elseif step = 6 // (Again)
      Call @PrepareKeyboardPreset2 // üéπ 2
      Call @RecordNextLoopInGroup2 // ‚è∫Ô∏è 2
    elseif step = 7
      Call @ToggleSendKeyboard // üéπ ‚úîÔ∏è
    
    // Verse
    elseif step = 8 // When the wicked‚Ä¶
      Call @IncreaseClockLength // ‚è≤Ô∏è 9
      Call @PrepareGuitarPreset1 // üé∏ 1
      Call @RecordNextLoopInGroup3 // ‚è∫Ô∏è 3
    elseif step = 9
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    elseif step = 10 // (Again)
      Call @PrepareGuitarPreset3 // üé∏ 3
      Call @RecordNextLoopInGroup3 // ‚è∫Ô∏è 3
    elseif step = 11
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    
    // Intro
    elseif step = 12 // Mmmh (deep voice)
      Call @DecreaseClockLength // ‚è≤Ô∏è 8
      Call @PrepareKeyboardPreset3 // üéπ 3
      Call @RecordNextLoopInGroup1 // ‚è∫Ô∏è 1
    elseif step = 13
      Call @ToggleSendKeyboard // üéπ ‚úîÔ∏è
    elseif step = 14 // Uuuh (add spontaneous hailings)
      Call @PrepareGuitarPreset3 // üé∏ 3
      Call @RecordNextLoopInGroup1 // ‚è∫Ô∏è 1
    elseif step = 15
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    
    // Bridge
    elseif step = 16 // Let the words‚Ä¶ (deep voice)
      Call @PrepareGuitarPreset2 // üé∏ 2
      Call @RecordNextLoopInGroup4 // ‚è∫Ô∏è 4
    elseif step = 17
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    elseif step = 18 // Let the words‚Ä¶ (high voice)
      Call @PrepareKeyboardPreset1 // üéπ 1
      Call @RecordNextLoopInGroup4 // ‚è∫Ô∏è 4
    elseif step = 19
      Call @ToggleSendKeyboard // üéπ ‚úîÔ∏è
    
    // Chorus
    elseif step = 20 // By the rivers‚Ä¶
      Call @PrepareGuitarPreset3 // üé∏ 3
      Call @RecordNextLoopInGroup2 // ‚è∫Ô∏è 2
    elseif step = 21
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    elseif step = 22 // (Again)
      Call @PrepareKeyboardPreset1 // üéπ 1
      Call @RecordNextLoopInGroup2 // ‚è∫Ô∏è 2
    elseif step = 23
      Call @ToggleSendKeyboard // üéπ ‚úîÔ∏è
    
      Call @Error
    endif

  //////////////////
  // Pink Floyd - Wish you were here
  //////////////////
  elseif preset = 2
    // Intro
    if step = 1 // 1st guitar (epic riff)
      Call @ToggleSendMicrophone // üé§ ‚ùå
      Call @PrepareGuitarPreset3 // üé∏ 3
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    
      Call @IncreaseClockLength // ‚è≤Ô∏è 9
      Call @IncreaseClockLength // ‚è≤Ô∏è 10
      Call @RecordNextLoopInGroup1 // ‚è∫Ô∏è 1
    elseif step = 2 // 2nd guitar
      Call @RecordNextLoopInGroup1 // ‚è∫Ô∏è 1
      Call @PrepareGuitarPreset1 // üé∏ 1
    elseif step = 3
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    
    // Verse 1
    elseif step = 4 // So, so you think you can tell
      Call @DecreaseClockLength // ‚è≤Ô∏è 9
      Call @DecreaseClockLength // ‚è≤Ô∏è 8
    
      Call @RecordNextLoopInGroup2 // ‚è∫Ô∏è 2
      Call @PrepareGuitarPreset1 // üé∏ 1
    elseif step = 5
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    elseif step = 6 // Did they get you to trade
      Call @PrepareGuitarPreset3 // üé∏ 3
      Call @RecordNextLoopInGroup2 // ‚è∫Ô∏è 2
    elseif step = 7
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    
    // Intro
    elseif step = 8 // Drums (1 bar, then ‚Äúdideld√º‚Äù and keyboard)
      Call @ToggleAndSelectGroup1 // ‚ñ∂Ô∏è 1
      Call @DecreaseClockLength // ‚è≤Ô∏è 7
      Call @DecreaseClockLength // ‚è≤Ô∏è 6
      Call @DecreaseClockLength // ‚è≤Ô∏è 5
      Call @DecreaseClockLength // ‚è≤Ô∏è 4
      Call @DecreaseClockLength // ‚è≤Ô∏è 3
      Call @DecreaseClockLength // ‚è≤Ô∏è 2
      Call @DecreaseClockLength // ‚è≤Ô∏è 1
    
      Call @RecordNextLoopInGroup0 // ‚è∫Ô∏è 0
      Call @PrepareKeyboardPreset1 // üéπ 1
    elseif step = 9
      Call @ToggleSendMicrophone // üé§ ‚úîÔ∏è
      Call @ToggleSendKeyboard // üéπ ‚úîÔ∏è
    
    // Verse 2 (or chorus)
    elseif step = 10 // How I wish‚Ä¶ (1st voice)
      Call @IncreaseClockLength // ‚è≤Ô∏è 2
      Call @IncreaseClockLength // ‚è≤Ô∏è 3
      Call @IncreaseClockLength // ‚è≤Ô∏è 4
      Call @IncreaseClockLength // ‚è≤Ô∏è 5
      Call @IncreaseClockLength // ‚è≤Ô∏è 6
      Call @IncreaseClockLength // ‚è≤Ô∏è 7
      Call @IncreaseClockLength // ‚è≤Ô∏è 8
    
      Call @RecordNextLoopInGroup2 // ‚è∫Ô∏è 2
      Call @PrepareKeyboardPreset1 // üéπ 1
    elseif step = 11
      Call @ToggleSendKeyboard // üéπ ‚úîÔ∏è
    elseif step = 12 // (2nd voice)
      Call @RecordNextLoopInGroup2 // ‚è∫Ô∏è 2
      Call @PrepareKeyboardPreset2 // üéπ 2
    elseif step = 13
      Call @ToggleSendKeyboard // üéπ ‚úîÔ∏è
    elseif step = 14 // (3rd voice)
      Call @PrepareGuitarPreset3 // üé∏ 3
    elseif step = 15
      Call @ToggleSendGuitar // üé∏ ‚úîÔ∏è
    
    // Intro
    elseif step = 16 // Fade out
      Call @ToggleAndSelectGroup1 // ‚ñ∂Ô∏è 1
    elseif step = 17
    
      Call @Error
    endif

  endif
@End

@Beep
  sysexData = [SYSEX_ANNOUNCE_FEEDBACK, FEEDBACK_BEEP]
  SendSysex sysexData, 2
@End

@ResetGtl
  sysexData = [SYSEX_RESET]
  SendSysex sysexData, 1
@End

@ToggleAndSelectGroup1
  sysexData = [SYSEX_TOGGLE_AND_SELECT_GROUP, BB_BUTTON_A]
  SendSysex sysexData, 2
@End

@ToggleAndSelectGroup2
  sysexData = [SYSEX_TOGGLE_AND_SELECT_GROUP, BB_BUTTON_B]
  SendSysex sysexData, 2
@End

@ToggleAndSelectGroup3
  sysexData = [SYSEX_TOGGLE_AND_SELECT_GROUP, BB_BUTTON_C]
  SendSysex sysexData, 2
@End

@ToggleAndSelectGroup4
  sysexData = [SYSEX_TOGGLE_AND_SELECT_GROUP, BB_BUTTON_D]
  SendSysex sysexData, 2
@End

@ToggleSendMicrophone
  sysexData = [SYSEX_TOGGLE_SEND_MICROPHONE_TO_GTL]
  SendSysex sysexData, 1
@End

@ToggleSendKeyboard
  sysexData = [SYSEX_TOGGLE_SEND_KEYBOARD_TO_GTL]
  SendSysex sysexData, 1
@End

@ToggleSendGuitar
  sysexData = [SYSEX_TOGGLE_SEND_GUITAR_TO_GTL]
  SendSysex sysexData, 1
@End

@PrepareKeyboardPreset1
  sysexData = [SYSEX_SELECT_KEYBOARD_PRESET, BB_BUTTON_A]
  SendSysex sysexData, 2
@End

@PrepareKeyboardPreset2
  sysexData = [SYSEX_SELECT_KEYBOARD_PRESET, BB_BUTTON_B]
  SendSysex sysexData, 2
@End

@PrepareKeyboardPreset3
  sysexData = [SYSEX_SELECT_KEYBOARD_PRESET, BB_BUTTON_C]
  SendSysex sysexData, 2
@End

@PrepareGuitarPreset1
  sysexData = [SYSEX_SELECT_GUITAR_PRESET, BB_BUTTON_A]
  SendSysex sysexData, 2
@End

@PrepareGuitarPreset2
  sysexData = [SYSEX_SELECT_GUITAR_PRESET, BB_BUTTON_B]
  SendSysex sysexData, 2
@End

@PrepareGuitarPreset3
  sysexData = [SYSEX_SELECT_GUITAR_PRESET, BB_BUTTON_C]
  SendSysex sysexData, 2
@End

@RecordNextLoopInGroup0
  sysexData = [SYSEX_RECORD_NEXT_LOOP_IN_GROUP, BB_BUTTON_MASTER]
  SendSysex sysexData, 2
@End

@RecordNextLoopInGroup1
  sysexData = [SYSEX_RECORD_NEXT_LOOP_IN_GROUP, BB_BUTTON_A]
  SendSysex sysexData, 2
@End

@RecordNextLoopInGroup2
  sysexData = [SYSEX_RECORD_NEXT_LOOP_IN_GROUP, BB_BUTTON_B]
  SendSysex sysexData, 2
@End

@RecordNextLoopInGroup3
  sysexData = [SYSEX_RECORD_NEXT_LOOP_IN_GROUP, BB_BUTTON_C]
  SendSysex sysexData, 2
@End

@RecordNextLoopInGroup4
  sysexData = [SYSEX_RECORD_NEXT_LOOP_IN_GROUP, BB_BUTTON_D]
  SendSysex sysexData, 2
@End

@IncreaseClockLength
  sysexData = [SYSEX_INCREASE_CLOCK_LENGTH]
  SendSysex sysexData, 1
@End

@DecreaseClockLength
  sysexData = [SYSEX_DECREASE_CLOCK_LENGTH]
  SendSysex sysexData, 1
@End


//////////////////////////////////////////////////////////
// These constants are shared across all Mozaic scripts //
//////////////////////////////////////////////////////////

@InitSharedConstants
  UNDEFINED = -1

  BB_BUTTON_MASTER = 0
  BB_BUTTON_A = 1
  BB_BUTTON_B = 2
  BB_BUTTON_C = 3
  BB_BUTTON_D = 4

  BB_BUTTONS_MIDI[BB_BUTTON_A] = 60
  BB_BUTTONS_MIDI[BB_BUTTON_B] = 62
  BB_BUTTONS_MIDI[BB_BUTTON_C] = 64
  BB_BUTTONS_MIDI[BB_BUTTON_D] = 65

  TAP_SHORT  = 0    // Up to 299ms
  TAP_MEDIUM = 300  // Between 300 and 999ms
  TAP_LONG   = 1000 // 1000ms or more

  SYSEX_TOGGLE_SEND_MICROPHONE_TO_GTL = 0
  SYSEX_TOGGLE_SEND_GUITAR_TO_GTL = 1
  SYSEX_RECORD_NEXT_LOOP_IN_GROUP = 2
  SYSEX_TOGGLE_AND_SELECT_GROUP = 3
  SYSEX_RESET = 4
  SYSEX_DECREASE_CLOCK_LENGTH = 5
  SYSEX_INCREASE_CLOCK_LENGTH = 6
  SYSEX_SELECT_GUITAR_PRESET = 7
  SYSEX_SELECT_MICROPHONE_PRESET = 8
  SYSEX_ANNOUNCE_FEEDBACK = 9
  SYSEX_TOGGLE_SEND_KEYBOARD_TO_GTL = 12
  SYSEX_SELECT_KEYBOARD_PRESET = 13

  FEEDBACK_WELCOME = 10
  FEEDBACK_ACTIVATED = 17
  FEEDBACK_BEEP = 20
  FEEDBACK_PRESET = 22
  FEEDBACK_MODE = 24
@End
