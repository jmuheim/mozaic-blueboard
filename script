// Make sure to start the BlueBoard while pressing the B pad!
@OnLoad
  SetShortName {BlueBoard}

  // BlueBoard (bb) pads.
  // Each pad (A, B, C, D) offers one space which is activated by long-tapping.
  bbA_in = 60
  bbB_in = 62
  bbC_in = 64
  bbD_in = 65

  gtlChannel = 15 // Channel on which GTL should listen => should not be 0!
  bbChannel = 0   // BlueBoard's channel => do not change this!
  longTapTime = 250 // Time needed to execute a long-tap (in ms)
  spaceActivationTapTime = 1000 // Time needed to activate a space
  onOffDelay = 50 // Delay between on/off note send, see https://forum.audiob.us/discussion/comment/829929/#Comment_829929
  baseNote = -1
  noteToSend = -1

  Call @IlluminateAllButtons

  activeSpace = bbA_in
  Call @ActivateSpace
@End

@OnMidiNoteOn
  padPressedStartTime = SystemTime // Time at which pad was pressed down
@End

@OnMidiNoteOff
  padPressedTime = SystemTime - padPressedStartTime

  if padPressedTime >= spaceActivationTapTime
    activeSpace = MidiNote
    Call @ActivateSpace
  elseif padPressedTime >= longTapTime
    Call @LongTap
  Else
    Call @ShortTap
  Endif
@End

@ShortTap
  // Log {Short Tap: }, MidiNote

  if MidiNote = bbA_in
    noteToSend = baseNote + 1
  elseif MidiNote = bbB_in
    noteToSend = baseNote + 2
  elseif MidiNote = bbC_in
    noteToSend = baseNote + 3
  elseif MidiNote = bbD_in
    noteToSend = baseNote + 4
  endif

  Call @SendNoteFromCurrentSpace
@End

@LongTap
  // Log {Long Tap: }, MidiNote

  if MidiNote = bbA_in
    noteToSend = baseNote + 5
  elseif MidiNote = bbB_in
    noteToSend = baseNote + 6
  elseif MidiNote = bbC_in
    noteToSend = baseNote + 7
  elseif MidiNote = bbD_in
    noteToSend = baseNote + 8
  endif

  Call @SendNoteFromCurrentSpace
@End

@ActivateSpace
  Log {Activated space: }, activeSpace

  if activeSpace = bbA_in
    baseNote = 0
  elseif activeSpace = bbB_in
    baseNote = 32
  elseif activeSpace = bbC_in
    baseNote = 64
  elseif activeSpace = bbD_in
    baseNote = 96
  endif

  noteToSend = baseNote
  Call @SendNoteFromCurrentSpace
@End

@IlluminateAllButtons
  // It would have been nice to be able to illuminate specific buttons depending on the interaction with the BB.
  // But this seems to be buggy, see https://forum.audiob.us/discussion/comment/832104/#Comment_832104.
  // So we just keep all of them illuminated (for visibility purposes when using the BB in the dark).

  SendMidiNoteOn bbChannel, bbA_in, 100
  SendMidiNoteOn bbChannel, bbB_in, 100
  SendMidiNoteOn bbChannel, bbC_in, 100
  SendMidiNoteOn bbChannel, bbD_in, 100
@End

@SendNoteFromCurrentSpace
  SendMidiNoteOn gtlChannel, noteToSend, 100
  SendMidiNoteOff gtlChannel, noteToSend, 0, onOffDelay
  Log {Sent Midi: }, noteToSend
@End
